[intrinsic] Number : type
[intrinsic] String : type

Maybe : value => type {
  None
  Some value
}

As : input output => trait (input -> output)

-- Can't convert [`input`] to [`output`].
--
-- Converting [`input`] into [`output`] isn't supported.
[default] [error] instance (As input output)

instance (As value value) : value -> value

error :: String -> value where (value :: ())
error : message -> intrinsic "crash" message

todo :: value where (value :: ())
todo : error "not yet implemented"

assert :: String -> Boolean -> ()
assert : message -> condition -> if condition {()} {error message}

unreachable :: value where (value :: ())
unreachable : error "unreachable"

-- Represents the default or initial value of a type.
Initial : value => trait value

-- Can't create an initial [`value`] value.
--
-- This code only works with lists, maybes, and other types that have an
-- initial value.
[default] [error] instance (Initial value)

instance (Initial Number) : 0
instance (Initial String) : ""
instance (Initial Boolean) : False
instance (Initial (Maybe value)) : None

-- Send a message to the runtime.
external :: String input -> output where (output :: ())
external : message data -> intrinsic "external" message data

-- Display a dropdown with the provided options.
[intrinsic] Dropdown : type

dropdown :: String -> Dropdown
dropdown : value -> intrinsic "dropdown" value
